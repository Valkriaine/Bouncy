apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'


def libraryGroupId = 'com.factor'
def libraryArtifactId = 'bouncy'
def libraryVersion = '1.4'


task androidJavadocs(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
    exclude '**/R.html', '**/R.*.html', '**/index.html'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidJavadocsJar
                artifact androidSourcesJar
                groupId = libraryGroupId
                artifactId = libraryArtifactId
                version = libraryVersion
            }
        }
    }
}


bintray {
    user = 'valkriaine'
    key = 'f1f7a41c1fd31b7dca617842e31c660b61fc814d'
    publications = ['release']
    publish = true
    override = true

    pkg {
        repo = 'factor'
        name = libraryArtifactId
        userOrg = 'valkriaine'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Valkriaine/bouncy.git'
        publicDownloadNumbers = true
        version {
            name = '1.4'
            desc = '1.4 release'
            released  = new Date()
            vcsTag = '1.4'
        }
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        versionCode 5
        versionName "1.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0"
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}